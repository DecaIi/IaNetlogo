;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; Base :
;;   mem0 : [
;;     0 : coordonnées de la zone d'attente pour les robots explorers attaquants
;;   ]
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to goRedRocketLauncher
end

to goRedHarvester
end

to initRedRocketLauncher
end

to initRedHarvester
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;  -----------------------------------------------------------------------------------------------------------------------BASE-----------------------------------------------------------------------------------------------------------------------
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to initRedBase
  set mem0 (list (list (xcor + 10) ycor)) ;; Initialisation des coordonnées de la zone d'attente
  set mem1 (list 0 0 0 0 0 0 0 0 0)
  set mem2 1
  FB-team-base-createExplorerOfType 2
end

to goRedBase
  ifelse (mem2 < 10)
  [
    FB-team-base-createExplorerOfType 2
    set mem2 (mem2 + 1)
  ]
  [
    if (mem2 = 10)
    [
      FB-team-base-createExplorerOfType 3
      set mem2 (mem2 + 1)
    ]
  ]
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;; Fonctions utiles
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to-report FB-team-askBaseWaitingZoneCoordinates
  report item 0 mem0
end

to-report FB-team-getTypeOfNewRobot
  report item 8 mem1
end

to FB-team-setTypeOfNewRobot [ t ]
  set mem1 replace-item 8 mem1 t 
end

to FB-team-base-createExplorerOfType [ t ]
  FB-team-setTypeOfNewRobot t
  new-Explorer self
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;  -----------------------------------------------------------------------------------------------------------------------EXPLORERS------------------------------------------------------------------------------------------------------------------
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;  --------------------------------------------Explorer tous types--------------------------------------------------------
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to initRedExplorer
  ;; Obtention du type du robot à sa création
  let b min-one-of my-bases [ distance myself ]
  FB-team-init-type-and-state ([FB-team-getTypeOfNewRobot] of b) 0
  
  if (FB-team-getType = 2) ;; Type explorer attaquant/soldat
  [
    FB-team-initExplorer2
  ]
  
  if (FB-team-getType = 3) ;; Type explorer attaquant/chef
  [
    FB-team-initExplorer3
  ]
  
  if (FB-team-getType = 4) ;; Type explorer attaquant/groupe
  [
    FB-team-initExplorer4
  ]
end

to goRedExplorer
  if (FB-team-getType = 2) ;; Type explorer attaquant/soldat
  [
    FB-team-goExplorer2
  ]
  
  if (FB-team-getType = 3) ;; Type explorer attaquant/chef
  [
    FB-team-goExplorer3
  ]
  
  if (FB-team-getType = 4) ;; Type explorer attaquant/groupe
  [
    FB-team-goExplorer4
  ]
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;  Etats d'Explorer tous types
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Se dirige à côté de la base
;; PREREQUIS : coordonnées de la zone d'attente dans mem0
to FB-team-explorer-state-goNextToBase
  FB-team-move-towards-xy (item 0 mem0) (item 1 mem0)
end

;; Se dirige à vers la base adverse
;; PREREQUIS : coordonnées de la base amie dans mem0
to FB-team-explorer-state-goToEnemyBase
  FB-team-set-target-xy (xcor + 10) (item 1 mem0) ;; Se déplce uniquement sur x, à hauteur de la base
  FB-team-move-towards-target
end

;; Ecrase les plantations
to FB-team-explorer-state-trampleOnPlantation
  let s min-one-of perceive-seeds ennemy [distance myself]
  ifelse (s != nobody) ;; S'il y a des plantations, les écraser
  [
    FB-team-move-towards-t s
  ]
  [
    FB-team-wander-around-target 10 ;; Sinon bouger autour de la base adverse dans un rayon de 10
  ]
end

;; Suicide sur la base advers
;; PREREQUIS : coordonnées de la base ennemie dans mem0
to FB-team-explorer-state-suicideOnBase
  let b min-one-of (perceive-base ennemy) [ distance myself ]
  ifelse (b != nobody and (distance b) < 2) ;; Si on est assez proche de la base adverse, on fonce dessus sans s'arrêter
  [
    forward-move speed
  ]
  [
    FB-team-move-towards-target ;; Sinon on se dirige vers elle
  ]
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;  Fonctions de report d'états pour Explorer tous types
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to FB-team-explorer-setState-goNextToBase
  set mem1 replace-item 1 mem1 901 
end

to-report FB-team-explorer-onState-goNextToBase
  report item 1 mem1 = 901 
end

to FB-team-explorer-setState-goToEnemyBase
  set mem1 replace-item 1 mem1 902 
end

to-report FB-team-explorer-onState-goToEnemyBase
  report item 1 mem1 = 902 
end

to FB-team-explorer-setState-trampleOnPlantation
  set mem1 replace-item 1 mem1 903 
end

to-report FB-team-explorer-onState-trampleOnPlantation
  report item 1 mem1 = 903
end

to FB-team-explorer-setState-suicideOnBase
  set mem1 replace-item 1 mem1 904 
end

to-report FB-team-explorer-onState-suicideOnBase
  report item 1 mem1 = 904
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;  --------------------------------------------Explorer type 2 (Attaquant/Soldat)-----------------------------------------
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to FB-team-initExplorer2
  let b min-one-of my-bases [ distance myself ]
  set mem0 [item 0 mem0] of b
  FB-team-explorer-setState-goNextToBase
end

to FB-team-goExplorer2
  if (FB-team-explorer-onState-goNextToBase) 
  [
    FB-team-explorer-state-goNextToBase
    if ((distancexy (item 0 mem0) (item 1 mem0)) < 6) ;; On passe à l'état suivant si on est dans un rayon de 6 autour du point d'attente
    [
      FB-team-explorer-setState-waitForChief
    ]
  ]
  
  if (FB-team-explorer-onState-waitForChief) 
  [
    FB-team-explorer-state-waitForChief
    if (FB-team-explorer-chiefHasArrived) ;; On passe à l'état suivant si le chef est arrivé
    [
      FB-team-explorer-setState-followChief
    ]
  ]
  
  if (FB-team-explorer-onState-followChief)
  [
    FB-team-explorer-state-followChief
    ;; On passe à l'état suivant si notre énergie est inférieure à celle qui nous faudrait pour rejoindre la base adverse, avec une chance de nous tirer dessus de 25%
    ;; et une sécurité de 0.5
    if (energy < (distancexy (item 0 mem0) (item 1 mem0)) * (explorer-metabolism + missile-robot-damage / 4) + 0.5) 
    [
      FB-team-explorer-setState-suicideOnBase
    ]
  ]
  
  if (FB-team-explorer-onState-suicideOnBase)
  [
    FB-team-explorer-state-suicideOnBase
  ]
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;  Etats d'Explorer 2
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Attend le chef
;; PREREQUIS : coordonnées de la zone d'attente dans mem0
to FB-team-explorer-state-waitForChief
  FB-team-wander-around-xy (item 0 mem0) (item 1 mem0) 6
end

;; Suit le chef
to FB-team-explorer-state-followChief
  let chief FB-team-explorer-perceiveChief
  if (chief != nobody)
  [
    FB-team-follow-t chief
    FB-team-set-target-xy [item 0 mem0] of chief [item 1 mem0] of chief
  ]
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;  Fonctions de report d'états pour Explorer 2
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to FB-team-explorer-setState-waitForChief
  set mem1 replace-item 1 mem1 201 
end

to-report FB-team-explorer-onState-waitForChief
  report item 1 mem1 = 201 
end

to FB-team-explorer-setState-followChief
  set mem1 replace-item 1 mem1 202 
end

to-report FB-team-explorer-onState-followChief
  report item 1 mem1 = 202 
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;; Fonctions utiles
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Renvoie le chef le plus proche
to-report FB-team-explorer-perceiveChief
  let exs (perceive-specific-robots color Explorers) with [(item 0 mem1) = 3] ;; Perceive Explorers of type 3
  report min-one-of exs [distance myself]
end

;; Informe l'explorer que le chef est arrivé
to FB-team-explorer-informChiefArrived
  set mem2 1
end

;; Renvoie true si on a été informé de l'arrivée du chef
to-report FB-team-explorer-chiefHasArrived
  report mem2 = 1
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;  --------------------------------------------Explorer type 3 (Attaquant/Chef)-------------------------------------------
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to FB-team-initExplorer3
  let b min-one-of my-bases [ distance myself ]
  set mem0 [item 0 mem0] of b
  FB-team-explorer-setState-goNextToBase
end

to FB-team-goExplorer3
  if (FB-team-explorer-onState-goNextToBase) 
  [
    FB-team-explorer-state-goNextToBase
    if ((distancexy (item 0 mem0) (item 1 mem0)) < 2) ;; On passe à l'état suivant si on est dans un rayon de 2 du point d'attente
    [
      FB-team-explorer-setState-informSoldiersOfArrival
    ]
  ]
  
  if (FB-team-explorer-onState-informSoldiersOfArrival)
  [
    FB-team-explorer-state-informSoldiersOfArrival
    FB-team-explorer-setState-goToEnemyBase
  ]
  
  if (FB-team-explorer-onState-goToEnemyBase)
  [
    FB-team-explorer-state-goToEnemyBase
    let b min-one-of (perceive-base ennemy) [ distance myself ]
    if (b != nobody) ;; On passe à l'état suivant si on perçoit la plantation
    [
      FB-team-set-target-t b
      FB-team-explorer-setState-trampleOnPlantation
    ]
  ]
  
  if (FB-team-explorer-onState-trampleOnPlantation)
  [
    FB-team-explorer-state-trampleOnPlantation
    ;; On passe à l'état suivant si notre énergie est inférieure à celle qui nous faudrait pour rejoindre la base adverse, avec une chance de nous tirer dessus de 25%
    ;; et une sécurité de 0.5
    if (energy < (distancexy (item 0 mem0) (item 1 mem0)) * (explorer-metabolism + missile-robot-damage / 4) + 0.5)
    [
      FB-team-explorer-setState-suicideOnBase
    ]
  ]
  
  if (FB-team-explorer-onState-suicideOnBase)
  [
    FB-team-explorer-state-suicideOnBase
  ]
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;  Etats d'Explorer 3
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Informe les soldats de son arrivée
to FB-team-explorer-state-informSoldiersOfArrival
  let soldiers (perceive-specific-robots color Explorers) with [(item 0 mem1) = 2] ;; Perceive Explorers of type 2
  ask soldiers [ FB-team-explorer-informChiefArrived ]
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;  Fonctions de report d'états pour Explorer 3
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to FB-team-explorer-setState-informSoldiersOfArrival
  set mem1 replace-item 1 mem1 301 
end

to-report FB-team-explorer-onState-informSoldiersOfArrival
  report item 1 mem1 = 301 
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;  --------------------------------------------Explorer type 4 (Attaquant/Groupe)-----------------------------------------
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to FB-team-initExplorer4
  let b min-one-of my-bases [ distance myself ]
  set mem0 [item 0 mem0] of b
  FB-team-explorer-setState-goNextToBase
end

to FB-team-goExplorer4
  if (FB-team-explorer-onState-goNextToBase) 
  [
    FB-team-explorer-state-goNextToBase
    if ((distancexy (item 0 mem0) (item 1 mem0)) < 6) ;; On passe à l'état suivant si on est dans un rayon de 6 du point d'attente
    [
      FB-team-explorer-setState-waitForCompanions
    ]
  ]
  
  if (FB-team-explorer-onState-waitForCompanions) 
  [
    FB-team-explorer-state-waitForCompanions
    if (count (perceive-specific-robots color Explorers) with [(item 0 mem1) = 4] >= 10) ;; On passe à l'état suivant si il y a 10 companions au moins
    [
      FB-team-explorer-setState-goToEnemyBase
    ]
  ]
  
  if (FB-team-explorer-onState-goToEnemyBase)
  [
    FB-team-explorer-state-goToEnemyBase
    let b min-one-of (perceive-base ennemy) [ distance myself ]
    if (b != nobody) ;; On passe à l'état suivant si on perçoit la plantation
    [
      FB-team-set-target-t b
      FB-team-explorer-setState-trampleOnPlantation
    ]
  ]
  
  if (FB-team-explorer-onState-trampleOnPlantation)
  [
    FB-team-explorer-state-trampleOnPlantation
    ;; On passe à l'état suivant si notre énergie est inférieure à celle qui nous faudrait pour rejoindre la base adverse, avec une chance de nous tirer dessus de 25%
    ;; et une sécurité de 0.5
    if (energy < (distancexy (item 0 mem0) (item 1 mem0)) * (explorer-metabolism + missile-robot-damage / 4) + 0.5)
    [
      FB-team-explorer-setState-suicideOnBase
    ]
  ]
  
  if (FB-team-explorer-onState-suicideOnBase)
  [
    FB-team-explorer-state-suicideOnBase
  ]
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;  Etats d'Explorer 4
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

;; Attend ses companions
;; PREREQUIS : coordonnées du point d'attente dans mem0
to FB-team-explorer-state-waitForCompanions
  FB-team-wander-around-xy (item 0 mem0) (item 1 mem0) 6
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;  Fonctions de report d'états pour Explorer 4
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to FB-team-explorer-setState-waitForCompanions
  set mem1 replace-item 1 mem1 401 
end

to-report FB-team-explorer-onState-waitForCompanions
  report item 1 mem1 = 401 
end

;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;
;;
;;  -----------------------------------------------------------------------------------------------------------------------FONCTIONS UTILES-----------------------------------------------------------------------------------------------------------
;;
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;

to FB-team-init-type-and-state [ t s ]
  set mem1 (list t s)
end

to-report FB-team-getType
  report item 0 mem1
end

to FB-team-set-target-t [ t ]
  set mem0 replace-item 0 mem0 [xcor] of t 
  set mem0 replace-item 1 mem0 [ycor] of t
end 

to FB-team-set-target-xy [ x y ]
    set mem0 replace-item 0 mem0 x
    set mem0 replace-item 1 mem0 y
end

to FB-Team-move-forward
    let nbTurn 0
    ifelse(free-ahead? speed = nobody) 
    [
        forward-move speed
    ][
        rt 45 ;;regarde a droite 
        ifelse(free-ahead? speed = nobody)
        [
            forward-move speed ;; avance 
            rt -45         ;;remet toi dans la bonne direction
        ][
           rt -90          ;;regarde a gauche 
           ifelse(free-ahead? speed = nobody)
           [
            forward-move speed ;;avance
            rt 45          ;;remet toi dans la bonne direcion
           ][
               rt -45 ;;demis tours
               if (free-ahead? speed = nobody) [
                   forward-move speed ;; avance 
        ]
           ]  
        ]
    ]
end

to FB-team-move-towards-target
  FB-team-move-towards-xy (item 0 mem0) (item 1 mem0)  
end

to FB-team-move-towards-t [ t ]
  set heading towards t  
  Fb-Team-move-forward
end

to FB-team-move-towards-xy [ x y ]
  set heading towardsxy x y  
  Fb-Team-move-forward
end

to FB-team-wander-around-target [ radius ]
  FB-team-wander-around-xy (item 0 mem0) (item 1 mem0) radius
end

to FB-team-wander-around-xy [ x y radius ]
  ifelse ((distancexy x y) > radius)
  [
    FB-Team-move-towards-xy x y
  ]
  [
    rt random 91 - 45
    ; avance vers l'avant
    if (free-ahead? speed = nobody) [forward-move speed]
  ]
end

to FB-team-follow-t [ t ]
  set heading towards t
  set heading (heading + [heading] of t) / 2
  if (distance t > 1)
  [
    FB-Team-move-forward
  ]
end